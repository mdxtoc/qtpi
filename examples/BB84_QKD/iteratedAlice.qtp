proc Alice(M, N, w, hks, qc, bsc, log) = 
  (* M: message; N: number of qubits *)
  
  (* choose the basis and value for each qbit at random *)
  . (let bs = randbits N) 
  . (let vs = randbits N)
  . ‚ÅÅ1
  
  (* send Bob the qbits I chose *)
  . ùÑÜ (b,v) <- zip bs vs: (newq q = ket_of_bits b v) qc!q . _0
    
  (* tell each other the qbit bases we used - me first *)
  . (let h0 = hwc bs hks 0 w)
  . bsc!h0,bs                              (* send Bob my bases *)
  . bsc?(h1,bBs)                           (* receive his bases *)
  . ‚ÅÅ2
  
  (* pick out the values for which our bases agree *)
  . (let rvs = reconcile bBs bs vs)      
  . ‚ÅÅ3
  
  (* Now we both know the same _number_ of values. Bob sends me a mask of
     that number of bits, and a list of the values it picks out from his
     list.
   *)
  . bsc?(h2,mask)
  . bsc?(h3,checkbitsB)
  . ‚ÅÅ4
  
  (* test to see if Bob and I agree on the bits selected by his mask *)
  . (let checkbitsA = mask_filter 0b1 mask rvs)
  . (let q_check = checkbitsB=checkbitsA)
  
  (* test for classical interference *)
  . (let c_check = forall (checkhash hks w) (zip [1;2;3] (zip [h1;h2;h3] [bBs;mask;checkbitsB]))) 
  
  (* Because we allow experimentation with number of checkbits, nSigma etc. to 
     provoke extreme statistical variation, it's possible to end up with fewer
     codebits that we need, so we test to see if subtracting Bob's checkbits has left 
     us enough codebits to make new hash codes and to encrypt M.  
   *)
  . (let enough = length rvs >= length checkbitsA + 5*w + length M)
  
  . ‚ÅÅ5
  
  (* If Alice detects interference or doesn't have enough codebits she should go silent. 
     But in this simulation she sends an empty message, to allow Bob to log what he's 
     done and terminate.
   *)
    . if ¬¨(q_check && c_check && enough) then
        . bsc!tagged hks 4 w []
        . _0
      else
        (* The (secret) code bits are the ones Bob didn't mask. We take new WC hash codes 
           and a secret message code from the secret code bits.
         *)
        . (let codebits = mask_filter 0b0 mask rvs)
        . (let hks' = take (5*w) codebits)
        . (let code = take (length M) (drop (5*w) codebits))
        . (let encryptedM = xor_mask code M)
        . (let h4 = hwc encryptedM hks 4 w)
        . bsc!h4,encryptedM                  (* send the encrypted message *)
        . ‚ÅÅ7
        . _0

  with
    1: . log!bs . log!vs 
       . _0
    2: . log!h0 . log!bs                         
       . log!h1 . log!bBs 
       . _0 
    3: . log!rvs 
       . _0
    4: . log!h2 . log!mask
       . log!h3 . log!checkbitsB 
       . _0 
    5: . log![bool2bit q_check]
       . log![bool2bit c_check]
       . log![bool2bit enough]
       . _0
    7: . log!code
       . log!M
       . log!h4 . log!encryptedM
       . log!hks'
       . _0
