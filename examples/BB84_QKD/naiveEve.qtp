(* a naive eavesdropper. Sees Alice's bits but messes with them. *)
proc Eve (w, hksAE, hksEB, qA, qB, bsA, bsB, againA, againB, logAE,logEB) =
    (new measured) 
    (  CopyQBits([],qA,qB,bsA,measured)
     | measured?(bs)                    .   (* bs, vs are what I think I saw *)
       measured?(vs)                    .
       logEB!bs                         .
       logEB!vs                         .
      
       measured?(h)      . 
       measured?(basesA) . bsB!h,basesA .   (* bases A->B       *)
       bsB?(basesB)      . bsA!basesB   .   (* bases B->A       *)
       bsB?(mask)        . bsA!mask     .   (* mask B->A        *)
       bsB?(h,cs)        . bsA!h,cs     .   (* checkbits B->A   *)
       logEB!cs                         .
       
       (    bsA?(h,encrypted)           .   (* encrypted message A->B *)
              bsB!h,encrypted           .   (* send it on *) 
              logAE![bool2bit false]    .   (* not going round *) 
              logEB![bool2bit false]    .   (* either of us    *)   
              logAE!encrypted           . 
              logEB![bool2bit false]    .   (* can't decrypt *)
              logEB![]                  .   (* empty message *)
              logAE![]                  .   (* empty hash keys A<->E *)
              logEB![]                  .   (* empty hash keys E<->B *)
              _0
        <+> againA?(_)                  .   (* round again, both sides *)
              againB!()                 .
              logAE![bool2bit true]     .
              logEB![bool2bit true]     .
              Eve (w, hksAE, hksEB, qA, qB, bsA, bsB, againA, againB, logAE,logEB)
       )
    )
