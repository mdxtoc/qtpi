(* a sophisticated eavesdropper. Plays Bob to Alice, Alice to Bob *)

proc Eve (qcA, qcB, bcA, bcB, againA, againB, log) =
    (new copied) 
    (  CopyBits([],qcA,qcB,bcA,copied)
     | copied?(bs)                      .   (* bs, vs are what I _sent_ *)
       copied?(vs)                      .
            log!bs                   .
            log!vs                   .
      
       copied?(basesA) . bcA!bs         .   (* send my bases to Alice *)
        
       EveLoop (qcA, qcB, bcA, bcB, againA, againB, bs, vs, bs, vs, basesA, log)
    )
       
proc EveLoop (qcA, qcB, bcA, bcB, againA, againB, bsA, vsA, bsB, vsB, basesA, log) =
   bcB!bsB . bcB?(basesB)               .   (* send my bases to Bob; receive his in return *)
   (let rvsB = reconcile basesB bsB vsB)
   bcB?(maskB) . bcB?(checkbitsB)       .   (* receive Bob's mask and check bits *)
                                            (* but ignore his check bits *)
                log!checkbitsB       .
   (let codeB = mask_filter 0b0 maskB rvsB)
   (* if Alice has more code bits than Bob then she might send me a message
      that I couldn't recrypt. So make sure that doesn't happen.
    *)
   (let rvsA = reconcile bsA basesA vsA)
   (let diff = length rvsA - length codeB)
   if diff>=0 then
     (* fake a mask to give Alice the same number of code bits as Bob *)
     (let maskA = tabulate diff (const 0b1) @ tabulate (length codeB) (const 0b0))
     bcA!maskA                          .   (* send Alice the mask and checkbits *)
     bcA!mask_filter 0b1 maskA rvsA     .
     (    againA?(_)                    .   (* Alice didn't get enough code bits *)
            againB!()                   .   (* round again, Bob *)
                log![bool2bit true]  .
            Eve (qcA, qcB, bcA, bcB, againA, againB, log)
      <+> bcA?(encrypted)               .   (* receive her coded message *)
                log![bool2bit false] .
                log!encrypted        .
            (let codeA = mask_filter 0b0 maskA rvsA)
            (let message = xor_mask (take (length encrypted) codeA) encrypted)
                                                 (* decode as Bob would *)
                 log![bool2bit true] .   (* If ok, I got the message *)
                 log!message         .
            bcB!xor_mask (take (length message) codeB) message       
                                        .   (* encode for Bob as Alice would and send it to him *)
            _0
     )
   else                                     (* too many code bits with Bob *)
      againB!()                         .   (* round again, Bob *)
            log![bool2bit true]      .
      (* in case Bob is suspicious, send him new qbits *)
      (let bs = randbits (length bsB))
      (let vs = randbits (length bsB))
      (new sent)
      (  SendBits (zip bs vs, qcB, sent)
       | sent?(_)                       .
            log!bs                   .  
            log!vs                   .
         EveLoop (qcA, qcB, bcA, bcB, againA, againB, bsA, vsA, bs, vs, basesA, log)
      )
   fi
  
proc CopyBits (bvs, qcA, qcB, bcA, result) =
        qcA?(q)                                  . 
            (let b = randbit ())
            q=?[if b=0 then _I else _H fi](v)   . 
            qcB!q                                . 
            CopyBits ((b,v)::bvs, qcA, qcB, bcA, result)
    <+> bcA?(basesA)                            . 
            (let bvs = rev bvs)
            result!map fst bvs                  . 
            result!map snd bvs                  . 
            result!basesA                       .
            _0

