(* given a random qbit (a|0>+b|1>), this may terminate with
   either a|0>+b|1> or a|0>-b|1>. Interesting, eh? Only 
   'really' works if the initial qbit is |0> 
   -- i.e. an unfair coin IMHO.
 *)

P(fromQ:^qbit, toQ:^qbit, report) =     fromQ?(y) . toQ!y .         report!false . _0
                                    <+> fromQ?(y) . y>>_X . toQ!y . report!true  . _0
                     
Q(q: qbit, toP:^qbit, fromP:^qbit, report) = 
  report!qbit_state q  .
  q>>_H . toP!q . fromP?(q') . q'>>_H . 
  report!qbit_state q' . 
  _0 

System() = (let k = read_int "number of trials")
           (let sb = read_string "initial state (0,1,+,-)")
           (let bv = match sb .
                 "0" . |0> 
             <+> "1" . |1> 
             <+> "+" . |+>
             <+> "-" . |->
             <+> _   . abandon "bad initial state"
             hctam
           )
           (let verbose = match read_string "with commentary (y/n)" .
                              "y" . true
                          <+> "n" . false
                          <+> _   . abandon "pardon?"
                          hctam
           )
           Controller (k, bv, verbose)

Controller (k, bv, verbose) = Report (k, bv, verbose, 0, 0, 0)

Report (k, bv, verbose, i, score, nX) = 
  if i=k then {print_strings [string_of_value i; " trials, "; 
                              string_of_value nX; " flips; ";
                              string_of_value score; " successes\n"
                             ]} . _0
  else
    (newq q = bv) 
    (new s:^qbit, r:^qbit)
    (new reportP, reportQ)
    (  P(s,r,reportP) 
     | Q(q,s,r,reportQ) 
     | reportQ?(init)  .
       reportP?(b)     .
       reportQ?(final) .
       {if verbose && b then print_string "with a flip; " else () fi} .
       {if verbose then print_strings ["initially "; init; " finally "; final; "\n"]
        else () fi
       } .
       Report (k, bv, verbose, i+1, 
                               if init=final then score+1 else score fi,
                               if b then nX+1 else nX fi
              )
    )
  fi
