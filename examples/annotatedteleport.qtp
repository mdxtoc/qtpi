
proc Alice (x:qbit, c:^bit*bit) = 
    (newq z)
    out!["initially Alice's x is "] .
    outq!(qval x) . 
    out![" and z is "] . 
    outq!(qval z) . out!["\n"] .
    z,x>>CNot . 
    out!["after three-way entanglement z is "] . 
    outq!(qval z) . out!["\n"] .
    z-/-[H](vz) . 
    out!["z is measured in the Hadamard basis, giving ";
                    show vz; ";\n and now x is "] .
    outq!(qval x) . out!["\n"] .
    x-/-(vx) . 
    out!["x is measured in the computational basis, giving ";
                    show vx; "\n"] .
    c!vz,vx . 
    _0 

proc Bob(y:qbit, c:^bit*bit) = 
    c?(b1,b2) . 
    out!["Alice sends "; show b1; ",";
                    show b2; "\nBob's y is "] .
    outq!(qval y) . out!["\n"] .
    y >> match b1,b2 . + 0b0,0b0 . I
                       + 0b0,0b1 . X
                       + 0b1,0b0 . Z
                       + 0b1,0b1 . Z*X     . 
    out!["finally Bob's y is "] . 
    outq!(qval y) . out!["\n"] .
    _0 

proc System () = 
  (newq x=|+>, y=|0>) x,y>>CNot . 
  (new c:^bit*bit) | Alice(x,c) | Bob(y,c)
