proc Experiment (M, nWC, multiplier, reportA, reportB) = (new qc,bsc,again)
                                                         (  Alice (M,nWC,multiplier,qc,bsc,again,reportA) 
                                                          | Bob   (qc,bsc,again,reportB)
                                                         )
    
proc System() = (let n = read_min_int 1 "length of message")
                (let nWC = read_min_int 0 "length of a hash key")
                (let multiplier = read_min_int 3 "qbit multiplier")
                (let k = read_min_int 1 "number of trials")
                (let verbose = read_bool "with commentary" "y" "n")
                Report (k, verbose, n, nWC, multiplier)
           
proc Report (k, verbose, n, nWC, multiplier) = Controller (k, 0, verbose, 0, 0, 0, 0, 0, n, nWC, multiplier)

proc Controller (k, i, verbose, nOk, nIntf, nReps, sumChecks, minChecks, n, nWC, multiplier) =
  if i<k then {if k<>1 then print_strings ["trial number "; string_of_value (i+1); 
                                           if verbose then "\n" else " " fi] 
                       else () fi
              } .
              (let M = randbits n)
              {if verbose then print_strings ["message is "; string_of_value M; "\n"] else () fi} .
              (new reportA, reportB, doneA, doneB)
              (  Experiment (M, nWC, multiplier, reportA, reportB) 
               | ReportAlice (0, verbose, reportA, doneA)
               | ReportBob (verbose, reportB, doneB)
               | doneA?(intf_seen,nc,reps)   .
                 doneB?(messageB)       .
                 (let nIntf = if intf_seen then nIntf+1 else nIntf fi)
                 (let nOk = if not intf_seen && messageB=M then nOk+1 else nOk fi)
                 (let minChecks = if i=0 then nc else min nc minChecks fi)
                 Controller (k, i+1, verbose, nOk, nIntf, nReps+reps, sumChecks+nc, minChecks, n, nWC, multiplier)
              )
  else {print_strings ["all done: "; 
                       string_of_value nIntf;          " interfered with; ";
                       string_of_value nOk;            " exchanges succeeded; ";
                       string_of_value (k-nOk);        " failed; ";
                       string_of_value nReps;          " repetition(s); ";
                       "average check bits " ; string_of_value (sumChecks/k); 
                       " minimum check bits "; string_of_value minChecks; "\n"
                      ]
       } . 
       _0 
  fi
